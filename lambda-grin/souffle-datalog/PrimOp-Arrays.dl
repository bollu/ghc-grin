/*
  HINT: is interpreted -/+

  Arrays

  primop effectful
    + "newArray#" :: T_Int64 -> %a -> {"State#" %s} -> {"GHC.Prim.Unit#" {"MutableArray#" %s %a}}

  primop pure
    - "sameMutableArray#" :: {"MutableArray#" %s %a} -> {"MutableArray#" %s %a} -> T_Int64

  primop effectful
    + "readArray#"  :: {"MutableArray#" %s %a} -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" %a}
    + "writeArray#" :: {"MutableArray#" %s %a} -> T_Int64 -> %a -> {"State#" %s} -> {"GHC.Prim.(##)"}

  primop pure
    - "sizeofArray#"        :: {"Array#" %a} -> T_Int64
    - "sizeofMutableArray#" :: {"MutableArray#" %s %a} -> T_Int64
    + "indexArray#"         :: {"Array#" %a} -> T_Int64 -> {"GHC.Prim.Unit#" %a}

  primop effectful
    + "unsafeFreezeArray#" :: {"MutableArray#" %s %a} -> {"State#" %s} -> {"GHC.Prim.Unit#" {"Array#" %a}}
    + "unsafeThawArray#"   :: {"Array#" %a} -> {"State#" %s} -> {"GHC.Prim.Unit#" {"MutableArray#" %s %a}}
    + "copyArray#"         :: {"Array#" %a} -> T_Int64 -> {"MutableArray#" %s %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.(##)"}
    + "copyMutableArray#"  :: {"MutableArray#" %s %a} -> T_Int64 -> {"MutableArray#" %s %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.(##)"}
    + "cloneArray#"        :: {"Array#" %a} -> T_Int64 -> T_Int64 -> {"Array#" %a}
    + "cloneMutableArray#" :: {"MutableArray#" %s %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" {"MutableArray#" %s %a}}
    + "freezeArray#"       :: {"MutableArray#" %s %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" {"Array#" %a}}
    + "thawArray#"         :: {"Array#" %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" {"MutableArray#" %s %a}}
    + "casArray#"          :: {"MutableArray#" %s %a} -> T_Int64 -> %a -> %a -> {"State#" %s} -> {"GHC.Prim.(#,#)" T_Int64 %a}

  Small Arrays

  primop effectful
    + "newSmallArray#" :: T_Int64 -> %a -> {"State#" %s} -> {"GHC.Prim.Unit#" {"SmallMutableArray#" %s %a}}

  primop pure
    - "sameSmallMutableArray#" :: {"SmallMutableArray#" %s %a} -> {"SmallMutableArray#" %s %a} -> T_Int64

  primop effectful
    + "readSmallArray#"  :: {"SmallMutableArray#" %s %a} -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" %a}
    + "writeSmallArray#" :: {"SmallMutableArray#" %s %a} -> T_Int64 -> %a -> {"State#" %s} -> {"GHC.Prim.(##)"}

  primop pure
    - "sizeofSmallArray#"        :: {"SmallArray#" %a} -> T_Int64
    - "sizeofSmallMutableArray#" :: {"SmallMutableArray#" %s %a} -> T_Int64
    + "indexSmallArray#"         :: {"SmallArray#" %a} -> T_Int64 -> {"GHC.Prim.Unit#" %a}

  primop effectful
    + "unsafeFreezeSmallArray#" :: {"SmallMutableArray#" %s %a} -> {"State#" %s} -> {"GHC.Prim.Unit#" {"SmallArray#" %a}}
    + "unsafeThawSmallArray#"   :: {"SmallArray#" %a} -> {"State#" %s} -> {"GHC.Prim.Unit#" {"SmallMutableArray#" %s %a}}
    + "copySmallArray#"         :: {"SmallArray#" %a} -> T_Int64 -> {"SmallMutableArray#" %s %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.(##)"}
    + "copySmallMutableArray#"  :: {"SmallMutableArray#" %s %a} -> T_Int64 -> {"SmallMutableArray#" %s %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.(##)"}
    + "cloneSmallArray#"        :: {"SmallArray#" %a} -> T_Int64 -> T_Int64 -> {"SmallArray#" %a}
    + "cloneSmallMutableArray#" :: {"SmallMutableArray#" %s %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" {"SmallMutableArray#" %s %a}}
    + "freezeSmallArray#"       :: {"SmallMutableArray#" %s %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" {"SmallArray#" %a}}
    + "thawSmallArray#"         :: {"SmallArray#" %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" {"SmallMutableArray#" %s %a}}
    + "casSmallArray#"          :: {"SmallMutableArray#" %s %a} -> T_Int64 -> %a -> %a -> {"State#" %s} -> {"GHC.Prim.(#,#)" T_Int64 %a}

*/

/*
  Question:
    should we distinct Array and MutableArray?
    can we reuse Array for each container like primitives?

  Answer:
    At first I go with the simplest approach: single reusable Array.
*/

// IDEA: Array is a kind of domain specific TypeVarPointsTo
.decl Array(ext_result:Variable, ty_node:Variable, item:Variable)
.output Array

//////////////////////
.decl RetTup1Node0(ext:External, ty_node:Variable)
.decl RetTup(ext:External, tag:Tag, i:number, ty_node:Variable)

.output RetTup1Node0
.output RetTup

RetTup1Node0(ext, ty_node) :-
  ExternalReturnType(ext, ty_res),
  TypeNode(ty_res, "GHC.Prim.Unit#"),
  TypeNodeArgument(ty_res, 0, ty_node).

RetTup(ext, tag, i, ty_node) :-
  ExternalReturnType(ext, ty_res),
  TypeNode(ty_res, tag),
  TypeNodeArgument(ty_res, i, ty_node).

/////////////////////

// "newArray#"      :: T_Int64 -> %a -> {"State#" %s} -> {"GHC.Prim.Unit#" {"MutableArray#" %s %a}}
// "newSmallArray#" :: T_Int64 -> %a -> {"State#" %s} -> {"GHC.Prim.Unit#" {"SmallMutableArray#" %s %a}}
// new array
Array(r, ty_node, item) :-
  ( op = "newArray#"
  ; op = "newSmallArray#"
  ),
  Call(r, op, _),
  // initial item
  CallArgument(r, 1, item),
  // extract result node
  RetTup1Node0(op, ty_node).

// "readArray#"       :: {"MutableArray#" %s %a}      -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" %a}
// "readSmallArray#"  :: {"SmallMutableArray#" %s %a} -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" %a}
// "indexArray#"      :: {"Array#" %a}                -> T_Int64 -> {"GHC.Prim.Unit#" %a}
// "indexSmallArray#" :: {"SmallArray#" %a}           -> T_Int64 -> {"GHC.Prim.Unit#" %a}
// NOTE: have common implementation because they use the first argument and set the return value with the same abstract type
TypeVarPointsTo(r, ty_node, item) :-
  ( op = "readArray#"
  ; op = "readSmallArray#"
  ; op = "indexArray#"
  ; op = "indexSmallArray#"
  ),
  Call(r, op, _),
  // lookup array items
  CallArgument(r, 0, arr),
  ExternalOrigin(arr, ext_result, arr_node),
  Array(ext_result, arr_node, item),
  // lookup result node
  RetTup1Node0(op, ty_node).

// "writeArray#"      :: {"MutableArray#" %s %a}      -> T_Int64 -> %a -> {"State#" %s} -> {"GHC.Prim.(##)"}
// "writeSmallArray#" :: {"SmallMutableArray#" %s %a} -> T_Int64 -> %a -> {"State#" %s} -> {"GHC.Prim.(##)"}
// extend array
Array(ext_result, ty_node, item) :-
  (op = "writeArray#" ; op = "writeSmallArray#"),
  Call(r, op, _),
  // item to write
  CallArgument(r, 2, item),
  // lookup array
  CallArgument(r, 0, arr),
  ExternalOrigin(arr, ext_result, ty_node),
  // validation
  Array(ext_result, ty_node, _).

// "unsafeFreezeArray#"       :: {"MutableArray#" %s %a}      -> {"State#" %s} -> {"GHC.Prim.Unit#" {"Array#" %a}}
// "unsafeFreezeSmallArray#"  :: {"SmallMutableArray#" %s %a} -> {"State#" %s} -> {"GHC.Prim.Unit#" {"SmallArray#" %a}}
// "unsafeThawArray#"         :: {"Array#" %a}                -> {"State#" %s} -> {"GHC.Prim.Unit#" {"MutableArray#" %s %a}}
// "unsafeThawSmallArray#"    :: {"SmallArray#" %a}           -> {"State#" %s} -> {"GHC.Prim.Unit#" {"SmallMutableArray#" %s %a}}
// reuse array ; alias
TypeVarPointsTo(r, ty_node, arr) :- // bypass argument
  ( op = "unsafeFreezeArray#"
  ; op = "unsafeFreezeSmallArray#"
  ; op = "unsafeThawArray#"
  ; op = "unsafeThawSmallArray#"
  ),
  Call(r, op, _),
  // lookup array items
  CallArgument(r, 0, arr),
  // lookup result node
  RetTup1Node0(op, ty_node).

// "copyArray#"             :: {"Array#" %a}                -> T_Int64 -> {"MutableArray#" %s %a}      -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.(##)"}
// "copySmallArray#"        :: {"SmallArray#" %a}           -> T_Int64 -> {"SmallMutableArray#" %s %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.(##)"}
// "copyMutableArray#"      :: {"MutableArray#" %s %a}      -> T_Int64 -> {"MutableArray#" %s %a}      -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.(##)"}
// "copySmallMutableArray#" :: {"SmallMutableArray#" %s %a} -> T_Int64 -> {"SmallMutableArray#" %s %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.(##)"}
// extend array
Array(dst_ext_result, dst_arr_node, src_item) :-
  ( op = "copyArray#"
  ; op = "copySmallArray#"
  ; op = "copyMutableArray#"
  ; op = "copySmallMutableArray#"
  ),
  Call(r, op, _),
  // lookup src array items
  CallArgument(r, 0, src_arr),
  ExternalOrigin(src_arr, src_ext_result, src_arr_node),
  Array(src_ext_result, src_arr_node, src_item),
  // lookup dst array items
  CallArgument(r, 2, dst_arr),
  ExternalOrigin(dst_arr, dst_ext_result, dst_arr_node),
  Array(dst_ext_result, dst_arr_node, _).

// "cloneArray#"      :: {"Array#" %a}      -> T_Int64 -> T_Int64 -> {"Array#" %a}
// "cloneSmallArray#" :: {"SmallArray#" %a} -> T_Int64 -> T_Int64 -> {"SmallArray#" %a}
// new array
Array(r, arr_node, src_item) :-
  ( op = "cloneArray#"
  ; op = "cloneSmallArray#"
  ),
  Call(r, op, _),
  // lookup src array items
  CallArgument(r, 0, src_arr),
  ExternalOrigin(src_arr, src_ext_result, src_arr_node),
  Array(src_ext_result, src_arr_node, src_item),
  // extract result node
  ExternalReturnType(op, arr_node).

// "cloneMutableArray#"       :: {"MutableArray#" %s %a}      -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" {"MutableArray#" %s %a}}
// "cloneSmallMutableArray#"  :: {"SmallMutableArray#" %s %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" {"SmallMutableArray#" %s %a}}
// "freezeArray#"             :: {"MutableArray#" %s %a}      -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" {"Array#" %a}}
// "freezeSmallArray#"        :: {"SmallMutableArray#" %s %a} -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" {"SmallArray#" %a}}
// "thawArray#"               :: {"Array#" %a}                -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" {"MutableArray#" %s %a}}
// "thawSmallArray#"          :: {"SmallArray#" %a}           -> T_Int64 -> T_Int64 -> {"State#" %s} -> {"GHC.Prim.Unit#" {"SmallMutableArray#" %s %a}}
// new array
Array(r, arr_node, src_item) :-
  ( op = "cloneMutableArray#"
  ; op = "cloneSmallMutableArray#"
  ; op = "freezeArray#"
  ; op = "freezeSmallArray#"
  ; op = "thawArray#"
  ; op = "thawSmallArray#"
  ),
  Call(r, op, _),
  // lookup src array items
  CallArgument(r, 0, src_arr),
  ExternalOrigin(src_arr, src_ext_result, src_arr_node),
  Array(src_ext_result, src_arr_node, src_item),
  // extract result node
  RetTup1Node0(op, arr_node).

// "casArray#"      :: {"MutableArray#" %s %a}      -> T_Int64 -> %a -> %a -> {"State#" %s} -> {"GHC.Prim.(#,#)" T_Int64 %a}
// "casSmallArray#" :: {"SmallMutableArray#" %s %a} -> T_Int64 -> %a -> %a -> {"State#" %s} -> {"GHC.Prim.(#,#)" T_Int64 %a}
// extend array
TypeVarPointsTo(r, ty_node, new_item),
TypeVarPointsTo(r, ty_node, old_item),
Array(ext_result, arr_node, new_item) :-
  (op = "casArray#" ; op = "casSmallArray#"),
  Call(r, op, _),
  // old item
  CallArgument(r, 2, old_item),
  // new item
  CallArgument(r, 3, new_item),
  // lookup array
  CallArgument(r, 0, arr),
  ExternalOrigin(arr, ext_result, arr_node),
  // validation
  Array(ext_result, arr_node, _),
  // extract result node
  RetTup(op, "GHC.Prim.(#,#)", 1, ty_node).
